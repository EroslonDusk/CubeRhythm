########################################################
#                                                      #
#                     判定部分                         #
#                                                      #
########################################################

every ticks:
	{autoplay} is not "&a开"
	loop all players:
		{actor} is loop-player
		set {_entity} to target of loop-player ignoring blocks
		{_entity} is interaction
		set {_target} to location of {_entity}
		if metadata value "notetype" of {_entity} is "drag":
			set {_x} to z location of {_target} if metadata value "face" of {_entity} is "w" or "s"
			set {_x} to x location of {_target} if metadata value "face" of {_entity} is "a" or "d"
			set {_time} to ({_x} - 4) / {speed} * 50 if metadata value "face" of {_entity} is "w" or "a"
			set {_time} to (-1 * {_x} - 4) / {speed} * 50 if metadata value "face" of {_entity} is "s" or "d"
			if abs({_time}) <= 200:
				{_target}.add(0, 20, 0)
				teleport {_entity} to {_target}
				set metadata value "hited" of {_entity} to true
		exit loop

function exact(entity: entity):
	if metadata value "notetype" of {_entity} is not "hold":
		add 1 to {combo}
		play sound "entity.player.hurt" with pitch 2 to all players if {hitSound} is "&a开"
		add 1 to {hitNotes}
		add 1 to {statistics::exact}
	else:
		add {maxNote}*0.000001 to {hitNotes}
		add 1 to {statistics::exactHold}
	set {alphaTime} to 11
	set {score} to floor(1000000/{maxNote}*{hitNotes})
	set {_face} to metadata value "face" of {_entity}
	set {_loc} to location of {_entity}
	set yaw of {_loc} to 180 if {_face} is "w"
	set yaw of {_loc} to 90 if {_face} is "a"
	set yaw of {_loc} to 0 if {_face} is "s"
	set yaw of {_loc} to -90 if {_face} is "d"
	{_loc}.add(0, 0.5, 0)
	if metadata value "notetype" of {_entity} is "drag" or "hold" or "flick":
		if {autoplay} is not "&a开":
			{_loc}.add(0, -20, 0)
		else if metadata value "notetype" of {_entity} is "drag":
			{_loc}.add(0, 0, 0)
		else:
			{_loc}.add(0, -20, 0)
		#broadcast "%y location of {_loc}%"
	summon text display at {_loc}:
		set {_alpha} to 255
		set {_opa} to {_alpha} - 256 if {_alpha} > 127
		set display text of the entity to "&bExact"
		set display text background color of the entity to bukkitColor(0,0,0,0)
		set display scale of the entity to vector(1.5,1.5,1)
		set display text opacity of the entity to {_opa}
		set metadata value "belongsto" of the entity to uuid of {_ent1}
		set display shadow strength of the entity to 0
		set {_text} to the entity
	loop 20 times:
		{_loc}.add(0, 0.1, 0)
		teleport {_text} to {_loc}
		remove 12 from {_alpha}
		set {_opa} to {_alpha}
		set {_opa} to {_alpha} - 256 if {_alpha} > 127
		set display text opacity of {_text} to {_opa}
		wait 1 tick
	loop all text displays:
		loop-entity is {_text}
		delete loop-entity
		exit loop

function great(entity: entity, el: text):
	play sound "entity.player.hurt" with pitch 2 to all players if {hitSound} is "&a开"
	add 1 to {combo}
	add 0.7 to {hitNotes}
	add 1 to {statistics::just}
	set {alphaTime} to 11
	set {score} to floor(1000000/{maxNote}*{hitNotes})
	if {_el} is "early":
		send action bar "&bEarly" to all players
	if {_el} is "late":
		send action bar "&6Late" to all players
	set {_face} to metadata value "face" of {_entity}
	set {_loc} to location of {_entity}
	set yaw of {_loc} to 180 if {_face} is "w"
	set yaw of {_loc} to 90 if {_face} is "a"
	set yaw of {_loc} to 0 if {_face} is "s"
	set yaw of {_loc} to -90 if {_face} is "d"
	{_loc}.add(0, 0.5, 0)
	if metadata value "notetype" of {_entity} is "drag" or "hold" or "flick":
		{_loc}.add(0, -20, 0)
	summon text display at {_loc}:
		set {_alpha} to 255
		set {_opa} to {_alpha} - 256 if {_alpha} > 127
		set display text of the entity to "&eJust"
		set display text background color of the entity to bukkitColor(0,0,0,0)
		set display scale of the entity to vector(1.5,1.5,1)
		set display text opacity of the entity to {_opa}
		set metadata value "belongsto" of the entity to uuid of {_ent1}
		set display shadow strength of the entity to 0
		set {_text} to the entity
	loop 20 times:
		{_loc}.add(0, 0.1, 0)
		teleport {_text} to {_loc}
		remove 12 from {_alpha}
		set {_opa} to {_alpha}
		set {_opa} to {_alpha} - 256 if {_alpha} > 127
		set display text opacity of {_text} to {_opa}
		wait 1 tick
	loop all text displays:
		loop-entity is {_text}
		delete loop-entity
		exit loop

function miss():
	set {combo} to 0
	add 1 to {statistics::miss}
	send title "" with subtitle "&4Miss" to all players for 0.5 seconds with fade in 0.1 second and fade out 0.3 seconds

function hitNote(entity: entity):
	set {_loc} to location of {_entity}
	set {_x} to z location of {_loc} if metadata value "face" of {_entity} is "w" or "s"
	set {_x} to x location of {_loc} if metadata value "face" of {_entity} is "a" or "d"
	set {_time} to ({_x} - 4) / {speed} * 50 if metadata value "face" of {_entity} is "w" or "a"
	set {_time} to (-1 * {_x} - 4) / {speed} * 50 if metadata value "face" of {_entity} is "s" or "d"
	#broadcast "%{_time}%ms"
	if abs({_time}) <= 80:
		exact({_entity})
		set {_canRemove} to true
	else if abs({_time}) <= 200:
		if {_time} > 0:
			great({_entity}, "early")
		else:
			great({_entity}, "late")
		set {_canRemove} to true
	{_canRemove} is set
	if metadata value "notetype" of {_entity} is "double":
		if metadata value "linknote" of {_entity} is set:
			loop all interactions:
				uuid of loop-entity is metadata value "linknote" of {_entity}
				deleteNote(loop-entity)
				clear metadata value "linknote" of {_entity}
				exit this loop
		else:
			deleteNote({_entity})
	else:
		deleteNote({_entity})

function deleteNote(entity: entity):
	set {_uuid} to uuid of {_entity}
	set {_linkuuid} to metadata value "linkuuid" of {_entity}
	metadata value "linkuuid" of {_entity} is set
	loop all block displays:
		if uuid of loop-entity is {_linkuuid}:
			delete loop-entity
		if metadata value "note1" of loop-entity is {_uuid}:
			delete loop-entity
		if metadata value "note2" of loop-entity is {_uuid}:
			delete loop-entity
	loop all interactions:
		loop-entity is {_entity}
		delete loop-entity
	wait 1 tick
	loop all text displays:
		if metadata value "belongsto" of loop-entity is {_uuid}:
			#broadcast "deleted %loop-entity%"
			delete loop-entity
		if metadata value "linkbind" of loop-entity is {_linkuuid}:
			#broadcast "deleted %loop-entity%"
			delete loop-entity

function checkIfTurned(entity: entity):
	exact({_entity})
	set {_face} to metadata value "face" of {_entity}
	set {_turn} to metadata value "turn" of {_entity}
	{autoplay} is not "&a开"
	wait 0.5 seconds
	loop all players:
		{actor} is loop-player
		set {_yaw} to yaw of loop-player
		set {_yaw} to {_yaw} - 360 if {_yaw} > 180
		set {_yaw} to {_yaw} + 360 if {_yaw} < -180
		if {_face} is "w":
			if {_turn} is "left":
				if {_yaw} is between -135 and -45:
					send title "" with subtitle "&aPass" to all players for 0.5 seconds with fade in 0.1 second and fade out 0.3 seconds
				else:
					miss()
			else if {_turn} is "right":
				if {_yaw} is between 45 and 135:
					send title "" with subtitle "&aPass" to all players for 0.5 seconds with fade in 0.1 second and fade out 0.3 seconds
				else:
					miss()
		else if {_face} is "a":
			if {_turn} is "left":
				if {_yaw} is between 135 and 180:
					send title "" with subtitle "&aPass" to all players for 0.5 seconds with fade in 0.1 second and fade out 0.3 seconds
				else if {_yaw} is between -180 and -135:
					send title "" with subtitle "&aPass" to all players for 0.5 seconds with fade in 0.1 second and fade out 0.3 seconds
				else:
					miss()
			else if {_turn} is "right":
				if {_yaw} is between -45 and 45:
					send title "" with subtitle "&aPass" to all players for 0.5 seconds with fade in 0.1 second and fade out 0.3 seconds
				else:
					miss()
		else if {_face} is "s":
			if {_turn} is "left":
				if {_yaw} is between 45 and 135:
					send title "" with subtitle "&aPass" to all players for 0.5 seconds with fade in 0.1 second and fade out 0.3 seconds
				else:
					miss()
			else if {_turn} is "right":
				if {_yaw} is between -135 and -45:
					send title "" with subtitle "&aPass" to all players for 0.5 seconds with fade in 0.1 second and fade out 0.3 seconds
				else:
					miss()
		else if {_face} is "d":
			if {_turn} is "right":
				if {_yaw} is between 135 and 180:
					send title "" with subtitle "&aPass" to all players for 0.5 seconds with fade in 0.1 second and fade out 0.3 seconds
				else if {_yaw} is between -180 and -135:
					send title "" with subtitle "&aPass" to all players for 0.5 seconds with fade in 0.1 second and fade out 0.3 seconds
				else:
					miss()
			else if {_turn} is "left":
				if {_yaw} is between -45 and 45:
					send title "" with subtitle "&aPass" to all players for 0.5 seconds with fade in 0.1 second and fade out 0.3 seconds
				else:
					miss()
		exit loop

function deleteEntity(entity: entity):
	wait 1 tick
	loop all text displays:
		loop-entity is {_entity}
		delete loop-entity
		exit loop

#every ticks:
#	loop all players:
#		send action bar "%{timer}%" to loop-player