on load:
	set {editor::bpm} to 120 if {editor::bpm} is not set
	set {editor::tick} to 0 if {editor::tick} is not set
	set {editor::length} to 1 if {editor::length} is not set
	set {editor::preTime} to 0 if {editor::preTime} is not set
	

on swap hand items:
	cancel event
	if {editMode} is true:
		if player is not sneaking:
			add 1 to {editor::tick}
		else:
			remove 1 from {editor::tick}

command /editor [<text>] [<number>]:
	trigger:
		player's gamemode is not spectator
		if arg-1 is not set:
			if {editMode} is not set:
				delete {savedX::*}
				delete {savedY::*}
				delete {savedType::*}
				set {editMode} to true
				set {editor::noteType} to "tap"
				message "&a开启编辑模式"
				message ""
				message "&e/editor help &7-&f 查看命令帮助"
				message "&e/editor bpm <number> &7-&f 每分钟拍数"
				message "&b/step <integer> &7-&f /step 4 表示步长为1/4拍"
				message "&b/b <number> &7-&f 传送到某一拍"
				message "&e/editor pretime <number> &7-&f 将第1拍的时间设置为x秒，不是变速曲就设为0并且不要动"
				message ""
				message "&f/editor &c退出编辑模式"
				message ""
				clear player's inventory
				give light blue wool named "&b&lTap&e (右键切换)" to player
				give stick named "&d&l魔杖&e (右键放置，左键清空，Shift对齐网格)" to player
				give lime concrete named "&a&l步进1格" to player
				give red concrete named "&c&l后退1格" to player
				set blocks within location(3, 3, 4, world "world", 0, 0) and location(-3, -3, 4, world "world", 0, 0) to glass
			else:
				delete {editMode}
				message "&c关闭编辑模式"
				clear player's inventory
				delete {savedX::*}
				delete {savedY::*}
				delete {savedType::*}
				delete {editor::noteType}
				set blocks within location(3, 3, 4, world "world", 0, 0) and location(-3, -3, 4, world "world", 0, 0) to air
		else if arg-1 is "bpm":
			arg-2 is set
			set {editor::bpm} to arg-2
			send title "&a✔" with subtitle "&a &a" to player for 0.5 seconds with fade in 0.5 seconds and fade out 0.5 seconds
		else if arg-1 is "pretime":
			arg-2 is set
			set {editor::preTime} to arg-2
			send title "&a✔" with subtitle "&a &a" to player for 0.5 seconds with fade in 0.5 seconds and fade out 0.5 seconds
		else if arg-1 is "help":
			message ""
			message "&e/editor help &7-&f 查看命令帮助"
			message "&e/editor bpm <number> &7-&f 每分钟拍数"
			message "&b/step <integer> &7-&f /step 4 表示步长为1/4拍"
			message "&b/b <number> &7-&f 传送到某一拍"
			message "&e/editor pretime <number> &7-&f 将第1拍的时间设置为x秒，不是变速曲就设为0并且不要动"
			message ""
			message "&f/editor &c退出编辑模式"
			message ""

command /b <number>:
	trigger:
		player's gamemode is not spectator
		if {editMode} is set:
			argument is set
			set {editor::tick} to ((argument) - 1) * {editor::length}
			send title "&a✔" with subtitle "&a &a" to player for 0.5 seconds with fade in 0.5 seconds and fade out 0.5 seconds

command /step <integer>:
	trigger:
		player's gamemode is not spectator
		if {editMode} is set:
			argument is set
			set {_past} to {editor::length}
			set {editor::length} to argument
			set {editor::tick} to {editor::tick} * {editor::length} / {_past}
			send title "&a✔" with subtitle "&a &a" to player for 0.5 seconds with fade in 0.5 seconds and fade out 0.5 seconds

every 2 ticks:
	if {editMode} is set:
		send action bar "&6BPM: &f%{editor::bpm}% &8| &b位置: &f%floor({editor::tick}/{editor::length}) + 1% : %mod({editor::tick}, {editor::length})% / %{editor::length}% (%{editor::tick}/{editor::length}*(60/{editor::bpm})+{editor::preTime}%s) &8| &a步长: &f1 / %{editor::length}% &8| &f输入w/a/s/d生成 " to all players

on right click:
	{editMode} is set
	cancel event
	player's gamemode is not spectator
	event-block is glass
	if player's tool is light blue wool named "&b&lTap&e (右键切换)":
		set {editor::noteType} to "double"
		set player's tool to orange wool named "&6&lDouble&e (右键切换)"
	else if player's tool is orange wool named "&6&lDouble&e (右键切换)":
		set {editor::noteType} to "drag"
		set player's tool to yellow wool named "&e&lDrag&e (右键切换)"
	else if player's tool is yellow wool named "&e&lDrag&e (右键切换)":
		set {editor::noteType} to "hold"
		set player's tool to white wool named "&f&lHold&e (右键切换)"
	else if player's tool is white wool named "&f&lHold&e (右键切换)":
		set {editor::noteType} to "flickL"
		set player's tool to magenta wool named "&d&l← Flick&e (右键切换)"
	else if player's tool is magenta wool named "&d&l← Flick&e (右键切换)":
		set {editor::noteType} to "flickR"
		set player's tool to red wool named "&c&lFlick →&e (右键切换)"
	else if player's tool is red wool named "&c&lFlick →&e (右键切换)":
		set {editor::noteType} to "tap"
		set player's tool to light blue wool named "&b&lTap&e (右键切换)"
	else if player's tool is lime concrete:
		add 1 to {editor::tick}
	else if player's tool is red concrete:
		remove 1 from {editor::tick}

on right click:
	{editMode} is set
	cancel event
	player's gamemode is not spectator
	event-block is glass
	if player's tool is stick named "&d&l魔杖&e (右键放置，左键清空，Shift对齐网格)":
		set {_z} to 4
		set {_pz} to z location of player
		set {_v} to vector in direction of player
		set {_vx} to x of {_v}
		set {_vy} to y of {_v}
		set {_vz} to z of {_v}
		set {_rate} to abs({_z} - {_pz}) / {_vz}
		set {_vx} to {_vx} * {_rate} + x location of player
		if player is sneaking:
			set {_vy} to {_vy} * {_rate} + y location of player + 1.4
		else:
			set {_vy} to {_vy} * {_rate} + y location of player + 1.6
		set {_vz} to {_vz} * {_rate}
		if player is sneaking:
			set {_vx} to round({_vx}*2)/2
			set {_vy} to round({_vy}*2)/2
			set {_loc} to location({_vx}, {_vy}, {_z}, world "world", 0, 0)
		else:
			set {_loc} to location({_vx}, {_vy}, {_z}, world "world", 0, 0)
		send title "" with subtitle "%{_vx} - 0.5% , %{_vy} - 0.5%" to all players
	if {editor::noteType} is "tap":
		summon block display at {_loc}:
			set display block data of entity to light_blue_concrete
			set display scale of entity to vector(1,1,1)
			set glowing of entity to true
			set display translation of entity to vector(-0.5,-0.5,0.1)
		add {_vx} to {savedX::Tap::*}
		add {_vy} to {savedY::Tap::*}
	if {editor::noteType} is "double":
		summon block display at {_loc}:
			set display block data of entity to orange_concrete
			set display scale of entity to vector(1,1,1)
			set glowing of entity to true
			set display translation of entity to vector(-0.5,-0.5,0.1)
		add {_vx} to {savedX::Double::*}
		add {_vy} to {savedY::Double::*}
	if {editor::noteType} is "drag":
		summon block display at {_loc}:
			set display block data of entity to yellow_concrete
			set display scale of entity to vector(1,1,1)
			set glowing of entity to true
			set display translation of entity to vector(-0.5,-0.5,0.1)
		add {_vx} to {savedX::Drag::*}
		add {_vy} to {savedY::Drag::*}
	if {editor::noteType} is "hold":
		summon block display at {_loc}:
			set display block data of entity to white_concrete
			set display scale of entity to vector(1,1,1)
			set glowing of entity to true
			set display translation of entity to vector(-0.5,-0.5,0.1)
		add {_vx} to {savedX::Hold::*}
		add {_vy} to {savedY::Hold::*}
	if {editor::noteType} is "flickL":
		summon block display at {_loc}:
			set display block data of entity to magenta_concrete
			set display scale of entity to vector(1,1,1)
			set glowing of entity to true
			set display translation of entity to vector(-0.5,-0.5,0.1)
		add {_vx} to {savedX::Flick::*}
		add {_vy} to {savedY::Flick::*}
		set {savedType::Flick} to "left"
	if {editor::noteType} is "flickR":
		summon block display at {_loc}:
			set display block data of entity to red_concrete
			set display scale of entity to vector(1,1,1)
			set glowing of entity to true
			set display translation of entity to vector(-0.5,-0.5,0.1)
		add {_vx} to {savedX::Flick::*}
		add {_vy} to {savedY::Flick::*}
		set {savedType::Flick} to "right"

on left click:
	{editMode} is set
	cancel event
	player's gamemode is not spectator
	event-block is glass
	if player's tool is stick named "&d&l魔杖&e (右键放置，左键清空，Shift对齐网格)":
		delete {savedX::*}
		delete {savedY::*}
		delete {savedType::*}
		loop all block displays:
			delete loop-entity

on chat:
	{editMode} is set
	player's gamemode is not spectator
	cancel event
	set {_1} to chat message
	if {_1} is "w" or "a" or "s" or "d":
		set {_face} to {_1}
	set {_1} to {editor::tick}/{editor::length}*(60/{editor::bpm})+{editor::preTime}
	{_1} is set
	set {_gen} to "empty"
	if {savedX::Tap::1} is set:
		set {_gen} to "tap(%{_1}%, ""%{_face}%"", %{savedX::Tap::1} - 0.5%, %{savedY::Tap::1} - 0.5%, false)"
	if {savedX::Drag::1} is set:
		set {_gen} to "drag(%{_1}%, ""%{_face}%"", %{savedX::Drag::1} - 0.5%, %{savedY::Drag::1} - 0.5%, false)"
	if {savedX::Hold::1} is set:
		set {_gen} to "hold(%{_1}%, ""%{_face}%"", %{savedX::Hold::1} - 0.5%, %{savedY::Hold::1} - 0.5%, false)"
	if {savedX::Flick::1} is set:
		set {_gen} to "flick(%{_1}%, ""%{_face}%"", ""%{savedType::Flick}%"", false)"
	if {savedX::Double::1} is set:
		set {_gen} to "double(%{_1}%, ""%{_face}%"", %{savedX::Double::1} - 0.5%, %{savedY::Double::1} - 0.5%, %{savedX::Double::2} - 0.5%, %{savedY::Double::2} - 0.5%, false)"
	{_gen} is not "empty"
	if {_gen} contains "<none>":
		replace all "<none>" with "&e<none>&c" in {_gen}
		broadcast "&c✘ &c%{_gen}%"
		broadcast "&f你的音符格式出现了缺失，请修改音符再尝试生成"
	else:
		broadcast "&a✔ &b%{_gen}%" if {_gen} contains "tap"
		broadcast "&a✔ &e%{_gen}%" if {_gen} contains "drag"
		broadcast "&a✔ &f%{_gen}%" if {_gen} contains "hold"
		broadcast "&a✔ &d%{_gen}%" if {_gen} contains "flick"
		broadcast "&a✔ &6%{_gen}%" if {_gen} contains "double"
		log "%{_gen}%" to file "generated.log"

every ticks:
	{editMode} is set
	loop all players:
		loop-player's gamemode is not spectator
		tool of loop-player is stick named "&d&l魔杖&e (右键放置，左键清空，Shift对齐网格)"
		set {_z} to 4
		set {_pz} to z location of loop-player
		set {_v} to vector in direction of loop-player
		set {_vx} to x of {_v}
		set {_vy} to y of {_v}
		set {_vz} to z of {_v}
		set {_rate} to abs({_z} - {_pz}) / {_vz}
		set {_vx} to {_vx} * {_rate} + x location of loop-player
		if loop-player is sneaking:
			set {_vy} to {_vy} * {_rate} + y location of loop-player + 1.4
		else:
			set {_vy} to {_vy} * {_rate} + y location of loop-player + 1.6
		set {_vz} to {_vz} * {_rate}
		if loop-player is sneaking:
			set {_vx} to round({_vx}*2)/2
			set {_vy} to round({_vy}*2)/2
			draw 2 of dust using dustOption(yellow, 1) at location({_vx}, {_vy}, {_z}, world "world", 0, 0)
		else:
			draw 2 of dust using dustOption(lime, 1) at location({_vx}, {_vy}, {_z}, world "world", 0, 0)