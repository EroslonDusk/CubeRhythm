on load:
	set {editor::bpm} to 120 if {editor::bpm} is not set
	set {editor::tick} to 0 if {editor::tick} is not set
	set {editor::length} to 1 if {editor::length} is not set
	set {editor::preTime} to 0 if {editor::preTime} is not set

on player drop:
	cancel event

on swap hand items:
	cancel event
	if {editMode} is true:
		if {editor::glowing} is false:
			set {editor::glowing} to true
			play sound "ui.button.click" with pitch 2 to player
			send title "" with subtitle "&7发光: &a开" to player for 0.5 seconds with fade in 0 seconds and fade out 0.5 seconds
		else:
			set {editor::glowing} to false
			play sound "ui.button.click" with pitch 2 to player
			send title "" with subtitle "&7发光: &c关" to player for 0.5 seconds with fade in 0 seconds and fade out 0.5 seconds

command /editor [<text>] [<text>]:
	trigger:
		set {_arg2} to arg-2 parsed as number if arg-2 parsed as number is set
		player's gamemode is not spectator
		if arg-1 is not set:
			if {editMode} is not set:
				delete {editorSave::*}
				set {editMode} to true
				set {editor::noteType} to "tap"
				set {editor::face} to "w"
				set {editor::glowing} to false
				message "&a进入编辑模式"
				message ""
				message "&e/editor help &7-&f 查看命令帮助"
				message "&e/editor bpm <number> &7-&f 每分钟拍数"
				message "&e/editor execution <section名字不加{}> &7-&f 在当前时间点放置Execution音符"
				message "&e/editor tag <text> &7-&f 修改准星指向的音符的标签" 
				message "&b/step <integer> &7-&f /step 4 表示步长为1/4拍"
				message "&b/b <number> &7-&f 传送到某一拍"
				message "&e/editor pretime <number> &7-&f 将第1拍的时间设置为x秒，不是变速曲就设为0并且不要动"
				message "&a/gen &7-&f 清空已放置的音符并生成谱面代码"
				message ""
				message "&f/editor &c退出编辑模式"
				message ""
				message "&a按键操作:"
				message "&b[鼠标滚轮↑] &f步进一格"
				message "&b[鼠标滚轮↓] &f后退一格"
				message "&b[1] &f更改音符类型(默认为&bTap&f)"
				message "&b[9] &f更改判定面(默认为前)"
				message "&b[F] &f设置音符发光(默认为关)"
				message ""
				
				clear player's inventory
				set slot 0 of player to a light blue wool named "&b"
				set slot 4 of player to a stick named "&d&l魔杖&e (右键放置，左键删除，Shift对齐网格)"
				set slot 8 of player to a white stained glass named "&b"
				set hotbar slot of player to 4
				set blocks within location(3, 3, 4, world "world", 0, 0) and location(-3, -3, 4, world "world", 0, 0) to glass
			else:
				delete {editMode}
				message "&c离开编辑模式"
				clear player's inventory
				delete {editorSave::*}
				delete {editor::noteType}
				set blocks within location(3, 3, 4, world "world", 0, 0) and location(-3, -3, 4, world "world", 0, 0) to air
				loop all block displays:
					delete loop-entity
		
		else if arg-1 is "bpm":
			{_arg2} is set
			set {editor::bpm} to {_arg2}
			send title "&a✔" with subtitle "&a &a" to player for 0.5 seconds with fade in 0.5 seconds and fade out 0.5 seconds
		else if arg-1 is "pretime":
			{_arg2} is set
			set {editor::preTime} to {_arg2}
			send title "&a✔" with subtitle "&a &a" to player for 0.5 seconds with fade in 0.5 seconds and fade out 0.5 seconds
		else if arg-1 is "help":
			message ""
			message "&e/editor help &7-&f 查看命令帮助"
			message "&e/editor bpm <number> &7-&f 每分钟拍数"
			message "&e/editor execution <section名字不加{}> &7-&f 在当前时间点放置Execution音符"
			message "&e/editor tag <text> &7-&f 修改准星指向的音符的标签" 
			message "&b/step <integer> &7-&f /step 4 表示步长为1/4拍"
			message "&b/b <number> &7-&f 传送到某一拍"
			message "&e/editor pretime <number> &7-&f 将第1拍的时间设置为x秒，不是变速曲就设为0并且不要动"
			message "&a/gen &7-&f 清空已放置的音符并生成谱面代码"
			message ""
			message "&f/editor &c退出编辑模式"
			message ""
			message "&a按键操作:"
			message "&b[鼠标滚轮↑] &f步进一格"
			message "&b[鼠标滚轮↓] &f后退一格"
			message "&b[1] &f更改音符类型(默认为&bTap&f)"
			message "&b[9] &f更改判定面(默认为前)"
			message "&b[F] &f设置音符发光(默认为关)"
			message ""
		else if arg-1 is "regive":
			{editMode} is true
			clear player's inventory
			set slot 0 of player to a light blue wool named "&b"
			set slot 4 of player to a stick named "&d&l魔杖&e (右键放置，左键删除，Shift对齐网格)"
			set slot 8 of player to a white stained glass named "&b"
		else if arg-1 is "execution":
			{editMode} is true
			arg-2 is set
			send title "" with subtitle "{%arg-2%}" to player for 0.5 seconds with fade in 0 seconds and fade out 0.5 seconds
			set {_id} to random uuid
			set {_loc} to location(0.5, 0.5, 4, world "world", 0, 0)
			set {_arg22} to arg-2
			summon block display at {_loc}:
				set display block data of entity to black stained glass
				set display scale of entity to vector(1,1,1)
				set glowing of entity to true
				set display translation of entity to vector(-0.5,-0.5,0.1)
				set display glow color override of entity to bukkitColor(255,0,255,0)
				set display name of entity to "Section: {%{_arg22}%}"
				set metadata value "id" of entity to {_id}
				set {editorSave::type::%{_id}%} to "execution"
				set {editorSave::time::%{_id}%} to {editor::tick}/{editor::length}*(60/{editor::bpm})+{editor::preTime}
				set {editorSave::loc::%{_id}%} to {_loc}
				set {editorSave::section::%{_id}%} to {_arg22}
		else if arg-1 is "tag":
			{editMode} is true
			loop all block displays:
				metadata value "isPreview" of loop-entity is true
				set {_cursor} to loop-entity
			{_cursor} is set
			if distance between (nearest block display relative to {_cursor}) and {_cursor} <= 1:
				set {_id} to metadata value "id" of nearest block display relative to {_cursor}
				send title "" with subtitle "新增Tag: &a%arg-2%" to player for 0.5 seconds with fade in 0 seconds and fade out 0.5 seconds if arg-2 is set
				send title "" with subtitle "删除Tag: &c%{editorSave::tag::%{_id}%}%" to player for 0.5 seconds with fade in 0 seconds and fade out 0.5 seconds if arg-2 is not set
				set {editorSave::tag::%{_id}%} to arg-2 if arg-2 is set
				set {editorSave::tag::%{_id}%} to "" if arg-2 is not set
				if {editorSave::glowing::%{_id}%} is true:
					set display name of nearest block display relative to {_cursor} to "&aTag: %{editorSave::tag::%{_id}%}%" if {editorSave::tag::%{_id}%} is not ""
					set display name of nearest block display relative to {_cursor} to "&a+++" if {editorSave::tag::%{_id}%} is ""
				else:
					set display name of nearest block display relative to {_cursor} to "&fTag: %{editorSave::tag::%{_id}%}%" if {editorSave::tag::%{_id}%} is not ""
					reset display name of nearest block display relative to {_cursor} if {editorSave::tag::%{_id}%} is ""
				

command /b <number>:
	trigger:
		player's gamemode is not spectator
		if {editMode} is set:
			argument is set
			set {editor::tick} to ((argument) - 1) * {editor::length}
			send title "&a✔" with subtitle "&a &a" to player for 0.5 seconds with fade in 0.5 seconds and fade out 0.5 seconds

command /step <integer>:
	trigger:
		player's gamemode is not spectator
		if {editMode} is set:
			argument is set
			set {_past} to {editor::length}
			set {editor::length} to argument
			set {editor::tick} to {editor::tick} * {editor::length} / {_past}
			send title "&a✔" with subtitle "&a &a" to player for 0.5 seconds with fade in 0.5 seconds and fade out 0.5 seconds

every 2 ticks:
	if {editMode} is set:
		send action bar "&6BPM: &f%{editor::bpm}%   &b第 &f%floor({editor::tick}/{editor::length}) + 1%&7小节 &f%mod({editor::tick}, {editor::length})%&7/%{editor::length}%拍 &f(%{editor::tick}/{editor::length}*(60/{editor::bpm})+{editor::preTime}%s)   &f共计 %size of {editorSave::type::*}% Notes" to all players

function addTick(p: player, t: number):
	add {_t} to {editor::tick}
	set {_current} to {editor::tick}/{editor::length}*(60/{editor::bpm})+{editor::preTime}
	loop all block displays:
		metadata value "id" of loop-entity is set
		delete loop-entity
	loop {editorSave::time::*}:
		set {_id} to loop-index
		set {_time} to loop-value
		set {_z} to ({_time} - {_current}) * 20 * {speed::%uuid of {_p}%}
		{_z} >= -1
		{_z} <= 50
		set {_loc} to {editorSave::loc::%loop-index%}
		{_loc}.add(0, 0, {_z})
		summon block display at {_loc}:
			set display block data of entity to light_blue_concrete if {editorSave::type::%{_id}%} is "tap"
			set display block data of entity to orange_concrete if {editorSave::type::%{_id}%} is "double"
			set display block data of entity to yellow_concrete if {editorSave::type::%{_id}%} is "drag"
			set display block data of entity to white_concrete if {editorSave::type::%{_id}%} is "hold"
			set display block data of entity to magenta_concrete if {editorSave::type::%{_id}%} is "flickL"
			set display block data of entity to red_concrete if {editorSave::type::%{_id}%} is "flickR"
			set display block data of entity to black stained glass if {editorSave::type::%{_id}%} is "execution"
			set display scale of entity to vector(1,1,1)
			set display scale of entity to vector(1,1,3*{speed::%uuid of {_p}%}) if {editorSave::type::%{_id}%} is "hold"
			set glowing of entity to true
			set display translation of entity to vector(-0.5,-0.5,0.1)
			set display translation of entity to vector(-0.5,-0.5,0.2) if {editorSave::type::%{_id}%} is "hold"
			set display glow color override of entity to bukkitColor(255,255,255,255) if {editorSave::face::%{_id}%} is "w"
			set display glow color override of entity to bukkitColor(255,255,235,42) if {editorSave::face::%{_id}%} is "a"
			set display glow color override of entity to bukkitColor(255,255,150,0) if {editorSave::face::%{_id}%} is "s"
			set display glow color override of entity to bukkitColor(255,255,0,0) if {editorSave::face::%{_id}%} is "d"
			set display glow color override of entity to bukkitColor(255,0,255,0) if {editorSave::type::%{_id}%} is "execution"
			if {editorSave::type::%{_id}%} is "execution":
				set display name of entity to "Section: {%{editorSave::section::%{_id}%}%}"
			else if {editorSave::glowing::%{_id}%} is true:
				set display name of entity to "&aTag: %{editorSave::tag::%{_id}%}%" if {editorSave::tag::%{_id}%} is not ""
				set display name of entity to "&a+++" if {editorSave::tag::%{_id}%} is ""
			else:
				set display name of entity to "&fTag: %{editorSave::tag::%{_id}%}%" if {editorSave::tag::%{_id}%} is not ""
				reset display name of entity if {editorSave::tag::%{_id}%} is ""
			set metadata value "id" of entity to {_id}

on tool change:
	{editMode} is set
	cancel event
	player's gamemode is not spectator
	if event-slot is 1 or 2 or 3:
		addTick(player, 1)
		send title "" with subtitle "&a>>> &7&o#%floor({editor::tick}/{editor::length}) + 1% &f%mod({editor::tick}, {editor::length})%&7/%{editor::length}% &a>>>" to player for 0.5 seconds with fade in 0 seconds and fade out 0.5 seconds
		play sound "block.note_block.hat" with pitch 2 to player
	else if event-slot is 5 or 6 or 7:
		addTick(player, -1)
		send title "" with subtitle "&a<<< &7&o#%floor({editor::tick}/{editor::length}) + 1% &f%mod({editor::tick}, {editor::length})%&7/%{editor::length}% &a<<<" to player for 0.5 seconds with fade in 0 seconds and fade out 0.5 seconds
		play sound "block.note_block.hat" with pitch 1.5 to player
	else if event-slot is 0:
		play sound "ui.button.click" with pitch 2 to player
		if slot 0 of player is light blue wool named "&b":
			set {editor::noteType} to "double"
			set player's tool to orange wool named "&b"
			send title "" with subtitle "&6Double" to player for 0.5 seconds with fade in 0 seconds and fade out 0.5 seconds
		else if slot 0 of player is orange wool named "&b":
			set {editor::noteType} to "drag"
			set player's tool to yellow wool named "&b"
			send title "" with subtitle "&eDrag" to player for 0.5 seconds with fade in 0 seconds and fade out 0.5 seconds
		else if slot 0 of player is yellow wool named "&b":
			set {editor::noteType} to "hold"
			set player's tool to white wool named "&b"
			send title "" with subtitle "&fHold" to player for 0.5 seconds with fade in 0 seconds and fade out 0.5 seconds
		else if slot 0 of player is white wool named "&b":
			set {editor::noteType} to "flickL"
			set player's tool to magenta wool named "&b"
			send title "" with subtitle "&a← &dFlick" to player for 0.5 seconds with fade in 0 seconds and fade out 0.5 seconds
		else if slot 0 of player is magenta wool named "&b":
			set {editor::noteType} to "flickR"
			set player's tool to red wool named "&b"
			send title "" with subtitle "&cFlick &a→" to player for 0.5 seconds with fade in 0 seconds and fade out 0.5 seconds
		else if slot 0 of player is red wool named "&b":
			set {editor::noteType} to "tap"
			set player's tool to light blue wool named "&b"
			send title "" with subtitle "&bTap" to player for 0.5 seconds with fade in 0 seconds and fade out 0.5 seconds
	else if event-slot is 8:
		play sound "ui.button.click" with pitch 2 to player
		if slot 8 of player is white stained glass named "&b":
			set {editor::face} to "a"
			set player's tool to yellow stained glass named "&b"
			send title "" with subtitle "&7判定面: &e左" to player for 0.5 seconds with fade in 0 seconds and fade out 0.5 seconds
		else if slot 8 of player is yellow stained glass named "&b":
			set {editor::face} to "s"
			set player's tool to orange stained glass named "&b"
			send title "" with subtitle "&7判定面: &6后" to player for 0.5 seconds with fade in 0 seconds and fade out 0.5 seconds
		else if slot 8 of player is orange stained glass named "&b":
			set {editor::face} to "d"
			set player's tool to red stained glass named "&b"
			send title "" with subtitle "&7判定面: &c右" to player for 0.5 seconds with fade in 0 seconds and fade out 0.5 seconds
		else if slot 8 of player is red stained glass named "&b":
			set {editor::face} to "w"
			set player's tool to white stained glass named "&b"
			send title "" with subtitle "&7判定面: &f前" to player for 0.5 seconds with fade in 0 seconds and fade out 0.5 seconds

on right click:
	{editMode} is set
	cancel event
	player's gamemode is not spectator
	if player's tool is stick named "&d&l魔杖&e (右键放置，左键删除，Shift对齐网格)":
		set {_z} to 4
		set {_pz} to z location of player
		set {_v} to vector in direction of player
		set {_vx} to x of {_v}
		set {_vy} to y of {_v}
		set {_vz} to z of {_v}
		set {_rate} to abs({_z} - {_pz}) / {_vz}
		set {_vx} to {_vx} * {_rate} + x location of player
		if player is sneaking:
			set {_vy} to {_vy} * {_rate} + y location of player + 1.4
		else:
			set {_vy} to {_vy} * {_rate} + y location of player + 1.6
		set {_vz} to {_vz} * {_rate}
		{_vx} <= 4
		{_vy} <= 4
		{_vx} >= -3
		{_vy} >= -3
		if player is sneaking:
			set {_vx} to round({_vx}*2)/2
			set {_vy} to round({_vy}*2)/2
			set {_loc} to location({_vx}, {_vy}, {_z}, world "world", 0, 0)
		else:
			set {_loc} to location({_vx}, {_vy}, {_z}, world "world", 0, 0)
		send title "" with subtitle "%{_vx} - 0.5% , %{_vy} - 0.5%" to player for 0.5 seconds with fade in 0 seconds and fade out 0.5 seconds
		set {_id} to random uuid
		set {_length} to 3*{speed::%uuid of player%}
		summon block display at {_loc}:
			set display block data of entity to light_blue_concrete if {editor::noteType} is "tap"
			set display block data of entity to orange_concrete if {editor::noteType} is "double"
			set display block data of entity to yellow_concrete if {editor::noteType} is "drag"
			set display block data of entity to white_concrete if {editor::noteType} is "hold"
			set display block data of entity to magenta_concrete if {editor::noteType} is "flickL"
			set display block data of entity to red_concrete if {editor::noteType} is "flickR"
			set display scale of entity to vector(1,1,1)
			set glowing of entity to true
			set display translation of entity to vector(-0.5,-0.5,0.1)
			set display glow color override of entity to bukkitColor(255,255,255,255) if {editor::face} is "w"
			set display glow color override of entity to bukkitColor(255,255,235,42) if {editor::face} is "a"
			set display glow color override of entity to bukkitColor(255,255,150,0) if {editor::face} is "s"
			set display glow color override of entity to bukkitColor(255,255,0,0) if {editor::face} is "d"
			set display scale of entity to vector(1,1,{_length}) if {editor::noteType} is "hold"
			set display translation of entity to vector(-0.5,-0.5,0.2) if {editor::noteType} is "hold"
			set display name of entity to "&a+++" if {editor::glowing} is true
			set metadata value "id" of entity to {_id}
			set {editorSave::type::%{_id}%} to {editor::noteType}
			set {editorSave::time::%{_id}%} to {editor::tick}/{editor::length}*(60/{editor::bpm})+{editor::preTime}
			set {editorSave::loc::%{_id}%} to {_loc}
			set {editorSave::face::%{_id}%} to {editor::face}
			set {editorSave::turn::%{_id}%} to "left" if {editor::noteType} is "flickL"
			set {editorSave::turn::%{_id}%} to "right" if {editor::noteType} is "flickR"
			set {editorSave::glowing::%{_id}%} to {editor::glowing}
			set {editorSave::tag::%{_id}%} to ""

on left click:
	{editMode} is set
	cancel event
	player's gamemode is not spectator
	if player's tool is stick named "&d&l魔杖&e (右键放置，左键删除，Shift对齐网格)":
		loop all block displays:
			metadata value "isPreview" of loop-entity is true
			set {_cursor} to loop-entity
		{_cursor} is set
		if distance between (nearest block display relative to {_cursor}) and {_cursor} <= 1:
			set {_id} to metadata value "id" of nearest block display relative to {_cursor}
			delete {editorSave::type::%{_id}%}
			delete {editorSave::time::%{_id}%}
			delete {editorSave::loc::%{_id}%}
			delete {editorSave::face::%{_id}%}
			delete {editorSave::turn::%{_id}%}
			delete {editorSave::section::%{_id}%}
			delete {editorSave::glowing::%{_id}%}
			delete {editorSave::tag::%{_id}%}
			delete nearest block display relative to {_cursor}

command /gen:
	trigger:	
		{editMode} is set
		player's gamemode is not spectator
		size of {editorSave::type::*} > 0
		set {_sorted::*} to sorted indices of {editorSave::time::*} in ascending order
		set {_count} to 0
		loop {_sorted::*}:
			set {_id} to loop-value
			set {_x} to x location of {editorSave::loc::%{_id}%}
			set {_y} to y location of {editorSave::loc::%{_id}%}
			if {editorSave::type::%{_id}%} is "execution":
				set {_gen} to "%{editorSave::type::%{_id}%}%(%{editorSave::time::%{_id}%}%, {%{editorSave::section::%{_id}%}%})"
				delete {editorSave::type::%{_id}%}
				delete {editorSave::time::%{_id}%}
				delete {editorSave::loc::%{_id}%}
				delete {editorSave::section::%{_id}%}
			else if {editorSave::type::%{_id}%} is "tap" or "hold" or "drag":
				set {_gen} to "%{editorSave::type::%{_id}%}%(%{editorSave::time::%{_id}%}%, ""%{editorSave::face::%{_id}%}%"", %{_x} - 0.5%, %{_y} - 0.5%, %{editorSave::glowing::%{_id}%}%, ""%{editorSave::tag::%{_id}%}%"")"
				delete {editorSave::type::%{_id}%}
				delete {editorSave::time::%{_id}%}
				delete {editorSave::loc::%{_id}%}
				delete {editorSave::face::%{_id}%}
				delete {editorSave::turn::%{_id}%}
				delete {editorSave::glowing::%{_id}%}
				delete {editorSave::tag::%{_id}%}
			else if {editorSave::type::%{_id}%} is "flickR" or "flickL":
				set {_tempType1} to "flick"
				set {_gen} to "%{_tempType1}%(%{editorSave::time::%{_id}%}%, ""%{editorSave::face::%{_id}%}%"", ""%{editorSave::turn::%{_id}%}%"", %{editorSave::glowing::%{_id}%}%, ""%{editorSave::tag::%{_id}%}%"")"
				delete {editorSave::type::%{_id}%}
				delete {editorSave::time::%{_id}%}
				delete {editorSave::loc::%{_id}%}
				delete {editorSave::face::%{_id}%}
				delete {editorSave::turn::%{_id}%}
				delete {editorSave::glowing::%{_id}%}
				delete {editorSave::tag::%{_id}%}
			else if {editorSave::type::%{_id}%} is "double":
				set {_tempType} to {editorSave::type::%{_id}%}
				set {_tempTime} to {editorSave::time::%{_id}%}
				set {_tempFace} to {editorSave::face::%{_id}%}
				set {_tempGlowing} to {editorSave::glowing::%{_id}%}
				set {_tempTag} to {editorSave::tag::%{_id}%}
				set {_x1} to x location of {editorSave::loc::%{_id}%}
				set {_y1} to y location of {editorSave::loc::%{_id}%}
				delete {editorSave::type::%{_id}%}
				delete {editorSave::time::%{_id}%}
				delete {editorSave::loc::%{_id}%}
				delete {editorSave::face::%{_id}%}
				delete {editorSave::turn::%{_id}%}
				delete {editorSave::glowing::%{_id}%}
				delete {editorSave::tag::%{_id}%}
				delete {_x2}
				delete {_y2}
				loop {editorSave::type::*}:
					loop-value-2 is "double"
					{editorSave::time::%loop-index-2%} - {_tempTime} is between -0.05 and 0.05
					set {_x2} to x location of {editorSave::loc::%loop-index-2%}
					set {_y2} to y location of {editorSave::loc::%loop-index-2%}
					delete {editorSave::type::%loop-index-2%}
					delete {editorSave::time::%loop-index-2%}
					delete {editorSave::loc::%loop-index-2%}
					delete {editorSave::face::%loop-index-2%}
					delete {editorSave::turn::%loop-index-2%}
					delete {editorSave::glowing::%loop-index-2%}
					delete {editorSave::tag::%loop-index-2%}
					exit loop
				set {_gen} to "%{_tempType}%(%{_tempTime}%, ""%{_tempFace}%"", %{_x1} - 0.5%, %{_y1} - 0.5%, %{_x2} - 0.5%, %{_y2} - 0.5%, %{_tempGlowing}%, ""%{_tempTag}%"")"
			else:
				set {_gen} to "empty"
			if {_gen} contains "<none>":
				replace all "<none>" with "&e<none>&c" in {_gen}
				broadcast "&c✘ &c%{_gen}%"
			else:
				{_gen} is not "empty"
				add 1 to {_count}
				log "%{_gen}%" to file "generated.log"
		loop all block displays:
			metadata value "id" of loop-entity is set
			delete loop-entity
		delete {editorSave::*}
		send title "" with subtitle "&a已清空面板" to player for 0.5 seconds with fade in 0 seconds and fade out 0.5 seconds
		send "" to player
		send "&a处理完毕，成功生成了 %{_count}% 个音符" to player
		send "&7(前往 &fplugins\skript\logs\generated.log&7 查看生成结果)" to player
		send "" to player

every ticks:
	{editMode} is set
	loop all players:
		loop-player's gamemode is not spectator
		tool of loop-player is stick named "&d&l魔杖&e (右键放置，左键删除，Shift对齐网格)"
		set {_z} to 4
		set {_pz} to z location of loop-player
		set {_v} to vector in direction of loop-player
		set {_vx} to x of {_v}
		set {_vy} to y of {_v}
		set {_vz} to z of {_v}
		set {_rate} to abs({_z} - {_pz}) / {_vz}
		set {_vx} to {_vx} * {_rate} + x location of loop-player
		if loop-player is sneaking:
			set {_vy} to {_vy} * {_rate} + y location of loop-player + 1.4
		else:
			set {_vy} to {_vy} * {_rate} + y location of loop-player + 1.6
		set {_vz} to {_vz} * {_rate}
		loop all block displays:
			delete loop-entity-2 if metadata value "isPreview" of loop-entity-2 is true
		{_vx} <= 4
		{_vy} <= 4
		{_vx} >= -3
		{_vy} >= -3
		if loop-player is sneaking:
			set {_vx} to round({_vx}*2)/2
			set {_vy} to round({_vy}*2)/2
		set {_loc} to location({_vx}, {_vy}, {_z}, world "world", 0, 0)
		summon block display at {_loc}:
			set display block data of entity to light_blue_concrete if {editor::noteType} is "tap"
			set display block data of entity to orange_concrete if {editor::noteType} is "double"
			set display block data of entity to white_concrete if {editor::noteType} is "hold"
			set display block data of entity to yellow_concrete if {editor::noteType} is "drag"
			set display block data of entity to magenta_concrete if {editor::noteType} is "flickL"
			set display block data of entity to red_concrete if {editor::noteType} is "flickR"
			set display scale of entity to vector(1,1,1)
			set glowing of entity to true
			set display translation of entity to vector(-0.5,-0.5,0.1)
			set display glow color override of entity to bukkitColor(255,255,255,255) if {editor::face} is "w"
			set display glow color override of entity to bukkitColor(255,255,235,42) if {editor::face} is "a"
			set display glow color override of entity to bukkitColor(255,255,150,0) if {editor::face} is "s"
			set display glow color override of entity to bukkitColor(255,255,0,0) if {editor::face} is "d"
			set display name of entity to "&a+++" if {editor::glowing} is true
			set metadata value "isPreview" of entity to true